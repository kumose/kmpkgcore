string(COMPARE NOTEQUAL "${TARGET_TRIPLET}" "${HOST_TRIPLET}" KMPKG_CROSSCOMPILING)
#Helper variable to identify the Target system. KMPKG_TARGET_IS_<targetname>
if (NOT DEFINED KMPKG_CMAKE_SYSTEM_NAME OR KMPKG_CMAKE_SYSTEM_NAME STREQUAL "")
    set(KMPKG_TARGET_IS_WINDOWS ON)

    if(DEFINED KMPKG_XBOX_CONSOLE_TARGET AND NOT "${KMPKG_XBOX_CONSOLE_TARGET}" STREQUAL "")
        set(KMPKG_TARGET_IS_XBOX ON)
    endif()
elseif(KMPKG_CMAKE_SYSTEM_NAME STREQUAL "WindowsStore")
    set(KMPKG_TARGET_IS_WINDOWS ON)
    set(KMPKG_TARGET_IS_UWP ON)
elseif(KMPKG_CMAKE_SYSTEM_NAME STREQUAL "Darwin")
    set(KMPKG_TARGET_IS_OSX ON)
    set(KMPKG_TARGET_IS_APPLE ON)
elseif(KMPKG_CMAKE_SYSTEM_NAME STREQUAL "iOS")
    set(KMPKG_TARGET_IS_IOS ON)
    set(KMPKG_TARGET_IS_APPLE ON)
elseif(KMPKG_CMAKE_SYSTEM_NAME STREQUAL "watchOS")
    set(KMPKG_TARGET_IS_WATCHOS ON)
    set(KMPKG_TARGET_IS_APPLE ON)
elseif(KMPKG_CMAKE_SYSTEM_NAME STREQUAL "tvOS")
    set(KMPKG_TARGET_IS_TVOS ON)
    set(KMPKG_TARGET_IS_APPLE ON)
elseif(KMPKG_CMAKE_SYSTEM_NAME STREQUAL "visionOS")
    set(KMPKG_TARGET_IS_VISIONOS ON)
    set(KMPKG_TARGET_IS_APPLE ON)
elseif(KMPKG_CMAKE_SYSTEM_NAME STREQUAL "Linux")
    set(KMPKG_TARGET_IS_LINUX ON)
elseif(KMPKG_CMAKE_SYSTEM_NAME STREQUAL "Android")
    set(KMPKG_TARGET_IS_ANDROID ON)
elseif(KMPKG_CMAKE_SYSTEM_NAME STREQUAL "FreeBSD")
    set(KMPKG_TARGET_IS_FREEBSD ON)
    set(KMPKG_TARGET_IS_BSD ON)
elseif(KMPKG_CMAKE_SYSTEM_NAME STREQUAL "OpenBSD")
    set(KMPKG_TARGET_IS_OPENBSD ON)
    set(KMPKG_TARGET_IS_BSD ON)
elseif(KMPKG_CMAKE_SYSTEM_NAME STREQUAL "NetBSD")
    set(KMPKG_TARGET_IS_NETBSD ON)
    set(KMPKG_TARGET_IS_BSD ON)
elseif(KMPKG_CMAKE_SYSTEM_NAME STREQUAL "SunOS")
    set(KMPKG_TARGET_IS_SOLARIS ON)
elseif(KMPKG_CMAKE_SYSTEM_NAME STREQUAL "MinGW")
    set(KMPKG_TARGET_IS_WINDOWS ON)
    set(KMPKG_TARGET_IS_MINGW ON)
elseif(KMPKG_CMAKE_SYSTEM_NAME STREQUAL "Emscripten")
    set(KMPKG_TARGET_IS_EMSCRIPTEN ON)
endif()

#Helper variables to identify the host system name
if (CMAKE_HOST_WIN32)
    set(KMPKG_HOST_IS_WINDOWS ON)
elseif (CMAKE_HOST_SYSTEM_NAME STREQUAL "Darwin")
    set(KMPKG_HOST_IS_OSX ON)
elseif(CMAKE_HOST_SYSTEM_NAME STREQUAL "Linux")
    set(KMPKG_HOST_IS_LINUX ON)
elseif(CMAKE_HOST_SYSTEM_NAME STREQUAL "FreeBSD")
    set(KMPKG_HOST_IS_FREEBSD ON)
    set(KMPKG_HOST_IS_BSD ON)
elseif(CMAKE_HOST_SYSTEM_NAME STREQUAL "OpenBSD")
    set(KMPKG_HOST_IS_OPENBSD ON)
    set(KMPKG_HOST_IS_BSD ON)
elseif(CMAKE_HOST_SYSTEM_NAME STREQUAL "NetBSD")
    set(KMPKG_HOST_IS_NETBSD ON)
    set(KMPKG_HOST_IS_BSD ON)
elseif(CMAKE_HOST_SYSTEM_NAME STREQUAL "SunOS")
    set(KMPKG_HOST_IS_SOLARIS ON)
endif()

#Helper variable to identify the host path separator.
if(CMAKE_HOST_WIN32)
    set(KMPKG_HOST_PATH_SEPARATOR ";")
elseif(CMAKE_HOST_UNIX)
    set(KMPKG_HOST_PATH_SEPARATOR ":")
endif()

#Helper variables to identify executables on host/target
if(CMAKE_HOST_WIN32)
    set(KMPKG_HOST_EXECUTABLE_SUFFIX ".exe")
else()
    set(KMPKG_HOST_EXECUTABLE_SUFFIX "")
endif()
#set(CMAKE_EXECUTABLE_SUFFIX ${KMPKG_HOST_EXECUTABLE_SUFFIX}) not required by find_program

if(KMPKG_TARGET_IS_WINDOWS)
    set(KMPKG_TARGET_EXECUTABLE_SUFFIX ".exe")
else()
    set(KMPKG_TARGET_EXECUTABLE_SUFFIX "")
endif()

#Helper variables to identify bundles on host/target
if(KMPKG_HOST_IS_OSX)
    set(KMPKG_HOST_BUNDLE_SUFFIX ".app")
else()
    set(KMPKG_HOST_BUNDLE_SUFFIX "")
endif()

if(KMPKG_TARGET_IS_APPLE)
    set(KMPKG_TARGET_BUNDLE_SUFFIX ".app")
else()
    set(KMPKG_TARGET_BUNDLE_SUFFIX "")
endif()

#Helper variables for libraries
if(KMPKG_TARGET_IS_MINGW)
    set(KMPKG_TARGET_STATIC_LIBRARY_SUFFIX ".a")
    set(KMPKG_TARGET_IMPORT_LIBRARY_SUFFIX ".dll.a")
    set(KMPKG_TARGET_SHARED_LIBRARY_SUFFIX ".dll")
    set(KMPKG_TARGET_STATIC_LIBRARY_PREFIX "lib")
    set(KMPKG_TARGET_SHARED_LIBRARY_PREFIX "lib")
    set(KMPKG_TARGET_IMPORT_LIBRARY_PREFIX "lib")
    set(KMPKG_FIND_LIBRARY_SUFFIXES ".dll" ".dll.a" ".a" ".lib")
    set(KMPKG_FIND_LIBRARY_PREFIXES "lib" "")
elseif(KMPKG_TARGET_IS_WINDOWS)
    set(KMPKG_TARGET_STATIC_LIBRARY_SUFFIX ".lib")
    set(KMPKG_TARGET_IMPORT_LIBRARY_SUFFIX ".lib")
    set(KMPKG_TARGET_SHARED_LIBRARY_SUFFIX ".dll")
    set(KMPKG_TARGET_IMPORT_LIBRARY_SUFFIX ".lib")
    set(KMPKG_TARGET_STATIC_LIBRARY_PREFIX "")
    set(KMPKG_TARGET_SHARED_LIBRARY_PREFIX "")
    set(KMPKG_TARGET_IMPORT_LIBRARY_PREFIX "")
    set(KMPKG_FIND_LIBRARY_SUFFIXES ".lib" ".dll") #This is a slight modification to CMakes value which does not include ".dll".
    set(KMPKG_FIND_LIBRARY_PREFIXES "" "lib") #This is a slight modification to CMakes value which does not include "lib".
elseif(KMPKG_TARGET_IS_APPLE)
    set(KMPKG_TARGET_STATIC_LIBRARY_SUFFIX ".a")
    set(KMPKG_TARGET_IMPORT_LIBRARY_SUFFIX "")
    set(KMPKG_TARGET_SHARED_LIBRARY_SUFFIX ".dylib")
    set(KMPKG_TARGET_STATIC_LIBRARY_PREFIX "lib")
    set(KMPKG_TARGET_SHARED_LIBRARY_PREFIX "lib")
    set(KMPKG_FIND_LIBRARY_SUFFIXES ".tbd" ".dylib" ".so" ".a")
    set(KMPKG_FIND_LIBRARY_PREFIXES "lib" "")
else()
    set(KMPKG_TARGET_STATIC_LIBRARY_SUFFIX ".a")
    set(KMPKG_TARGET_IMPORT_LIBRARY_SUFFIX "")
    set(KMPKG_TARGET_SHARED_LIBRARY_SUFFIX ".so")
    set(KMPKG_TARGET_STATIC_LIBRARY_PREFIX "lib")
    set(KMPKG_TARGET_SHARED_LIBRARY_PREFIX "lib")
    set(KMPKG_FIND_LIBRARY_SUFFIXES ".so" ".a")
    set(KMPKG_FIND_LIBRARY_PREFIXES "lib" "")
endif()
#Setting these variables allows find_library to work in script mode and thus in portfiles!
#This allows us scale down on hardcoded target dependent paths in portfiles
set(CMAKE_STATIC_LIBRARY_SUFFIX "${KMPKG_TARGET_STATIC_LIBRARY_SUFFIX}")
set(CMAKE_SHARED_LIBRARY_SUFFIX "${KMPKG_TARGET_SHARED_LIBRARY_SUFFIX}")
set(CMAKE_IMPORT_LIBRARY_SUFFIX "${KMPKG_TARGET_IMPORT_LIBRARY_SUFFIX}")
set(CMAKE_STATIC_LIBRARY_PREFIX "${KMPKG_TARGET_STATIC_LIBRARY_PREFIX}")
set(CMAKE_SHARED_LIBRARY_PREFIX "${KMPKG_TARGET_SHARED_LIBRARY_PREFIX}")
set(CMAKE_IMPORT_LIBRARY_PREFIX "${KMPKG_TARGET_IMPORT_LIBRARY_PREFIX}")

set(CMAKE_FIND_LIBRARY_SUFFIXES "${KMPKG_FIND_LIBRARY_SUFFIXES}" CACHE INTERNAL "") # Required by find_library
set(CMAKE_FIND_LIBRARY_PREFIXES "${KMPKG_FIND_LIBRARY_PREFIXES}" CACHE INTERNAL "") # Required by find_library

# Append platform libraries to KMPKG_SYSTEM_LIBRARIES
# The variable are just appended to permit to custom triplets define the variable

# Platforms with libdl
if(KMPKG_TARGET_IS_LINUX OR KMPKG_TARGET_IS_ANDROID OR KMPKG_TARGET_IS_OSX)
    list(APPEND KMPKG_SYSTEM_LIBRARIES dl)
endif()

# Platforms with libm
if(KMPKG_TARGET_IS_LINUX OR KMPKG_TARGET_IS_ANDROID OR KMPKG_TARGET_IS_FREEBSD OR KMPKG_TARGET_IS_OPENBSD OR KMPKG_TARGET_IS_OSX OR KMPKG_TARGET_IS_MINGW)
    list(APPEND KMPKG_SYSTEM_LIBRARIES m)
endif()

# Platforms with pthread
if(KMPKG_TARGET_IS_LINUX OR KMPKG_TARGET_IS_ANDROID OR KMPKG_TARGET_IS_OSX OR KMPKG_TARGET_IS_FREEBSD OR KMPKG_TARGET_IS_OPENBSD OR KMPKG_TARGET_IS_MINGW)
    list(APPEND KMPKG_SYSTEM_LIBRARIES pthread)
endif()

# Platforms with libstdc++
if(KMPKG_TARGET_IS_LINUX OR KMPKG_TARGET_IS_ANDROID OR KMPKG_TARGET_IS_FREEBSD OR KMPKG_TARGET_IS_OPENBSD OR KMPKG_TARGET_IS_MINGW)
    list(APPEND KMPKG_SYSTEM_LIBRARIES [[stdc\+\+]])
endif()

# Platforms with libc++
if(KMPKG_TARGET_IS_OSX)
    list(APPEND KMPKG_SYSTEM_LIBRARIES [[c\+\+]])
endif()

# Platforms with librt
if(KMPKG_TARGET_IS_LINUX OR KMPKG_TARGET_IS_ANDROID OR KMPKG_TARGET_IS_OSX OR KMPKG_TARGET_IS_FREEBSD OR KMPKG_TARGET_IS_MINGW)
    list(APPEND KMPKG_SYSTEM_LIBRARIES rt)
endif()

# Platforms with GCC libs
if(KMPKG_TARGET_IS_LINUX OR KMPKG_TARGET_IS_ANDROID OR KMPKG_TARGET_IS_OSX OR KMPKG_TARGET_IS_FREEBSD OR KMPKG_TARGET_IS_OPENBSD OR KMPKG_TARGET_IS_MINGW)
    list(APPEND KMPKG_SYSTEM_LIBRARIES gcc)
    list(APPEND KMPKG_SYSTEM_LIBRARIES gcc_s)
endif()

# Platforms with system iconv
if(KMPKG_TARGET_IS_OSX)
    list(APPEND KMPKG_SYSTEM_LIBRARIES iconv)
endif()

# Windows system libs
if(KMPKG_TARGET_IS_WINDOWS)
    list(APPEND KMPKG_SYSTEM_LIBRARIES advapi32)
    list(APPEND KMPKG_SYSTEM_LIBRARIES bcrypt)
    list(APPEND KMPKG_SYSTEM_LIBRARIES dinput8)
    list(APPEND KMPKG_SYSTEM_LIBRARIES gdi32)
    list(APPEND KMPKG_SYSTEM_LIBRARIES imm32)
    list(APPEND KMPKG_SYSTEM_LIBRARIES oleaut32)
    list(APPEND KMPKG_SYSTEM_LIBRARIES ole32)
    list(APPEND KMPKG_SYSTEM_LIBRARIES psapi)
    list(APPEND KMPKG_SYSTEM_LIBRARIES secur32)
    list(APPEND KMPKG_SYSTEM_LIBRARIES setupapi)
    list(APPEND KMPKG_SYSTEM_LIBRARIES shell32)
    list(APPEND KMPKG_SYSTEM_LIBRARIES shlwapi)
    list(APPEND KMPKG_SYSTEM_LIBRARIES strmiids)
    list(APPEND KMPKG_SYSTEM_LIBRARIES user32)
    list(APPEND KMPKG_SYSTEM_LIBRARIES uuid)
    list(APPEND KMPKG_SYSTEM_LIBRARIES version)
    list(APPEND KMPKG_SYSTEM_LIBRARIES vfw32)
    list(APPEND KMPKG_SYSTEM_LIBRARIES winmm)
    list(APPEND KMPKG_SYSTEM_LIBRARIES wsock32)
    list(APPEND KMPKG_SYSTEM_LIBRARIES Ws2_32)
    list(APPEND KMPKG_SYSTEM_LIBRARIES wldap32)
    list(APPEND KMPKG_SYSTEM_LIBRARIES crypt32)
endif()
