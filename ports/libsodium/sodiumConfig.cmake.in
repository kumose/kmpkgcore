
if("@KMPKG_LIBRARY_LINKAGE@" STREQUAL "static" AND NOT WIN32)
    include(CMakeFindDependencyMacro)
    find_dependency(Threads)
endif()

if(NOT TARGET unofficial-sodium::sodium)
    add_library(unofficial-sodium::sodium UNKNOWN IMPORTED)

    set_target_properties(unofficial-sodium::sodium PROPERTIES
        INTERFACE_INCLUDE_DIRECTORIES "${_KMPKG_INSTALLED_DIR}/${KMPKG_TARGET_TRIPLET}/include"
        IMPORTED_LINK_INTERFACE_LANGUAGES "C"
    )

    if("@KMPKG_LIBRARY_LINKAGE@" STREQUAL "static" AND NOT WIN32)
        set_target_properties(unofficial-sodium::sodium PROPERTIES
            INTERFACE_LINK_LIBRARIES Threads::Threads
        )
    endif()

    find_library(KMPKG_SODIUM_LIBRARY_RELEASE NAMES sodium libsodium PATHS "${_KMPKG_INSTALLED_DIR}/${KMPKG_TARGET_TRIPLET}/lib" NO_DEFAULT_PATH)
    if(EXISTS "${KMPKG_SODIUM_LIBRARY_RELEASE}")
        set_property(TARGET unofficial-sodium::sodium APPEND PROPERTY IMPORTED_CONFIGURATIONS "Release")
        set_target_properties(unofficial-sodium::sodium PROPERTIES IMPORTED_LOCATION_RELEASE "${KMPKG_SODIUM_LIBRARY_RELEASE}")
    endif()

    find_library(KMPKG_SODIUM_LIBRARY_DEBUG NAMES sodium libsodium PATHS "${_KMPKG_INSTALLED_DIR}/${KMPKG_TARGET_TRIPLET}/debug/lib" NO_DEFAULT_PATH)
    if(EXISTS "${KMPKG_SODIUM_LIBRARY_DEBUG}")
        set_property(TARGET unofficial-sodium::sodium APPEND PROPERTY IMPORTED_CONFIGURATIONS "Debug")
        set_target_properties(unofficial-sodium::sodium PROPERTIES IMPORTED_LOCATION_DEBUG "${KMPKG_SODIUM_LIBRARY_DEBUG}")
    endif()
endif()
